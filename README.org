* circe-notifications

[[./screenshot.png]]

Inspired by [[https://github.com/tomterl][tomterl]] on =#emacs-circe= who posted
[[https://github.com/jorgenschaefer/circe/wiki/Configuration#tracking-send-desktop-notifications-on-buffer-activity][this]]
configuration snippet, and Julien Danjou's
[[http://julien.danjou.info/blog/2012/erc-notifications][erc-desktop-notifications]].

** Features

   - Adds desktop notifications to
     [[https://github.com/jorgenschaefer/circe][Circe]].
   - Won't send a notification unless the channel is in =tracking-buffers=,
     i.e. not currently visible.
   - Avoids getting spammed by one nickname if they are messaging you a lot.
     After the first notification from some-user you will not get another
     notification from them until =circe-notifications-wait-for= seconds have
     elapsed.
   - Optional list of strings which can trigger a notification.
   - Optionally detects whether Emacs is focused by the window manager.  For
     example, if you're reading some documentation in Firefox and Emacs is
     hidden you may wish to receive notifications even for buffers that would be
     visible in Emacs.  This feature requires either both =xdotool= and
     =xorg-xprop= or OSX.
   - Works in OSX with =growlnotify=, =osascript= or =terminal-notifier=.

** Configuration

*** GNU/Linux
#+begin_src emacs-lisp :tangle yes
(autoload 'enable-circe-notifications "circe-notifications" nil t)

(eval-after-load "circe-notifications"
  '(setq setq circe-notifications-watch-strings
      '("people" "you" "like" "to" "hear" "from")))

(add-hook 'circe-server-connected-hook 'enable-circe-notifications)
#+end_src

Uses =dbus= to connect to your DE/WM's particular notification system.

If you like to use Emacs in a terminal emulator and that terminal sets =WM_NAME=
to something containing the string "emacs" you may wish to =(setq
circe-notifications-term-name "my-favourite-terminal")= so that
=circe-notifications-emacs-focused-p= will recognise Emacs running in that
terminal emulator.  This particular feature depends on =xdotool= and =xprop=.

*** OSX

#+begin_src emacs-lisp :tangle yes
(autoload 'enable-circe-notifications "circe-notifications" nil t)

(eval-after-load "circe-notifications"
  '(setq circe-notifications-watch-strings
         '("people" "you" "like" "to" "hear" "from")
         circe-notifications-backend "terminal-notifier"))

(add-hook 'circe-server-connected-hook 'enable-circe-notifications)
#+end_src

When =(string-equal system-type "darwin")= , =circe-notifications-backend= will
automagically be set to =osascript=, but there is also support for =growlnotify=
and [[https://github.com/alloy/terminal-notifier][terminal-notifier]].

** Caveats

If you use the configuration above with something like ZNC you will endure a
deluge of notifications upon buffer playback.

Short of patching a new RPL code into both ZNC and Circe specifically for this,
the only way to deal with it is to count occurrences of =(string-equal command
"NOTICE")= and =(string-equal host "znc.in")= for each network on ZNC and
=(add-hook 'circe-receive-message-functions 'circe-notifications)= after that.

The number of notices depends on how many networks you are connecting to and how
many lines are in ZNC's MOTD.  It's a massive kludge but here's how I do it.
Replace the line =(add-hook 'circe-server-connected-hook
'enable-circe-notifications)= with this:

#+begin_src emacs-lisp :tangle yes
;; Warning: this is very dumb
;;
;; ZNC's MOTD is 25 lines.
;; I have two networks defined in `circe-network-options'.
;; So wait to see 50 notices from ZNC before enabling notifications.

(defvar eqyiel-circe-znc-notices 0
  "How many notices have we received from ZNC?")

(defvar eqyiel-circe-znc-motd-length 25
  "How many lines are in ZNC's MOTD?")

(defun eqyiel-circe-znc-count-networks ()
  "Return the number of networks in `circe-network-options' multiplied by
`eqyiel-circe-znc-motd-length', so we can know how many notices to expect before
enabling notifications."
  (* eqyiel-circe-znc-motd-length (length circe-network-options)))

(defun eqyiel-circe-wait-for-znc (nick userhost _command target text)
  "If this TEXT from NICK and USERHOST looks like a line of ZNC's MOTD,
increment `eqyiel-circe-znc-notices', and enable notifications if there have
been at least `eqyiel-circe-znc-count-networks' `eqyiel-circe-znc-notices'."
  (when (and (string-equal nick "*status")
             (string-equal userhost "znc@znc.in"))
    (setq eqyiel-circe-znc-notices (+ 1 eqyiel-circe-znc-notices))
    (message "That's %d ..." eqyiel-circe-znc-notices)
    (when (<= (eqyiel-circe-znc-count-networks) eqyiel-circe-znc-notices)
      (message "OK.")
      (advice-remove 'circe-display-NOTICE 'eqyiel-circe-wait-for-znc)
      (enable-circe-notifications))))

(defun eqyiel-enable-circe-notifications ()
  (interactive)
  (advice-add 'circe-display-NOTICE :after 'eqyiel-circe-wait-for-znc)
  (advice-add 'circe-reconnect-all :before
              'eqyiel-disable-circe-notifications))

(eval-after-load 'circe
  '(eqyiel-enable-circe-notifications))

(defun eqyiel-disable-circe-notifications ()
  (interactive)
  (disable-circe-notifications)
  (setq eqyiel-circe-znc-notices 0)
  (advice-add 'circe-display-NOTICE :after 'eqyiel-circe-wait-for-znc))
#+end_src

Alternatively, you can =M-x enable-circe-notifications= after the buffer
playback is done.
